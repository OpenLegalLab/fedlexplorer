PREFIX jolux: <http://data.legilux.public.lu/resource/ontology/jolux#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

select ?dateApplicability ?rs ?droit ?title {
  {
    select (str(?dateApplicabilityNode) as ?dateApplicability) ?lang ?consolidationAbstract  {
      values (?startDate ?endDate ?lang) {
        ('2023-09-01' '2023-09-30' <http://publications.europa.eu/resource/authority/language/FRA> )
      }
      ?consolidation a jolux:Consolidation ;
                     jolux:dateApplicability ?dateApplicabilityNode ;
                     jolux:isMemberOf ?consolidationAbstract .
      filter(xsd:date(?dateApplicabilityNode) >= xsd:date(?startDate) && xsd:date(?dateApplicabilityNode) <= xsd:date(?endDate))
    }
  } 

  ?consolidationAbstract jolux:classifiedByTaxonomyEntry/skos:notation ?rsNode . 
  optional {
    ?consolidationAbstract jolux:isRealizedBy ?expression .
    ?expression jolux:language ?lang ;
                jolux:title ?title .
  }
  #filter(datatype(?rsNode) = <https://fedlex.data.admin.ch/vocabulary/notation-type/id-systematique>)
  bind(str(?rsNode) as ?rs)
  bind(if(strstarts(?rs, '0.'), 'International', 'Interne') as ?droit)
}
order by ?dateApplicability